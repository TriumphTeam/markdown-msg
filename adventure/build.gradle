plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

group 'me.mattstudios'
version parent.version

dependencies {

    api project(":msg")
    implementation "org.jetbrains:annotations:17.0.0"
    compileOnly "net.kyori:adventure-api:4.0.0-SNAPSHOT"

}

shadowJar {
    relocate("org.commonmark", "me.mattstudios.mfmsg.commonmark")
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "mf-msg"

            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = "Matt\'s Markdown JSON message"
                description = "Message framework for markdown like syntax to be converted to Minecraft."
                url = "https://github.com/ipsk/MF-MSG"

                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }

                developers {
                    developer {
                        id = "matt"
                        name = "Mateus Moreira"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/ipsk/MF-MSG.git"
                    developerConnection = "scm:git:ssh://github.com:ipsk/MF-MSG.git"
                    url = "http://github.com/ipsk/MF-MSG"
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }

            def releasesRepoUrl = repoUrl
            def snapshotsRepoUrl = repoSnapshotsUrl

            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

